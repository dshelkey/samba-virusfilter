## configure.in
## ======================================================================

# FIXME
AC_PREREQ(2.54)

AC_INIT(samba-antivirus, 0.1.0)

AC_CONFIG_HEADER(include/sav-config.h)
AC_SUBST(CONFIG_HEADERS)
CONFIG_HEADERS="$ac_config_headers"

AC_CONFIG_FILES(
  Makefile
  build/Makefile.common
  build/Makefile.package
  build/Makefile.top
  build/subst
)

## Installation directories
## ======================================================================

#AC_SUBST(package_subdir)
#package_subdir=
#AC_ARG_WITH(
#  subdir,
#  AC_HELP_STRING(--with-subdir=DIR, change default subdirectory used for installs @<:@/chimera@:>@),
#  [case "$withval" in
#    no) package_subdir=""
#      ;;
#    yes)
#      ;;
#    /*|\\*)
#      package_subdir="$withval"
#      ;;
#    *)
#      package_subdir="/$withval"
#      ;;
#    esac
#  ])

## Perl
## ======================================================================

AC_SUBST(PERL_COMMAND)
AC_ARG_WITH(
  perl,
  AC_HELP_STRING(--with-perl=PATH, [Use specific perl command]),
  [case "$withval" in
    yes|no)
      ;;
    *)
      PERL_COMMAND=$withval
      AC_MSG_RESULT([using $PERL_COMMAND for perl])
      ;;
    esac
  ],
  [AC_PATH_PROG(PERL_COMMAND, perl, not found)]
)

## Samba
## ======================================================================

AC_SUBST(SAMBA_SOURCE_DIR)
AC_ARG_WITH(
  samba-source,
  AC_HELP_STRING(--with-samba-source=PATH, [Where is the Samba source directory]),
  [case "$withval" in
    yes|no)
      ;;
    *)
      SAMBA_SOURCE_DIR=$withval
      AC_MSG_RESULT([using $SAMBA_SOURCE_DIR for the Samba source])
      ;;
    esac
  ],
  []
)

if test -z "$SAMBA_SOURCE_DIR"; then
  AC_ERROR([You must specify --with-samba-source=PATH])
fi

if test -f "$SAMBA_SOURCE_DIR/include/config.h"; then
  : OK
else
  AC_ERROR([Samba source has not been configured: $SAMBA_SOURCE_DIR])
fi

AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,prefix,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,exec_prefix,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,CONFIGDIR,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,BINDIR,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,LIBDIR,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,DATADIR,SAMBA_)
case "$SAMBA_DATADIR" in
*/samba)
  ## OK
  ;;
*)
  SAMBA_DATADIR="$SAMBA_DATADIR/samba"
  ;;
esac
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,MODULESDIR,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,VFSLIBDIR,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,LOCKDIR,SAMBA_)

AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,CC,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,CPPFLAGS,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,CFLAGS,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,PICFLAG,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,SHLD,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,SHLD_MODULE,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,LDSHFLAGS,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,DSO_EXPORTS,SAMBA_)
AC_GET_MAKEFILE_VAR(${SAMBA_SOURCE_DIR}/Makefile,DSO_EXPORTS_CMD,SAMBA_)

AC_GET_DEFINED_VAR(${SAMBA_SOURCE_DIR}/include/version.h,SAMBA_VERSION_MAJOR,)
AC_GET_DEFINED_VAR(${SAMBA_SOURCE_DIR}/include/version.h,SAMBA_VERSION_MINOR,)
AC_GET_DEFINED_VAR(${SAMBA_SOURCE_DIR}/include/version.h,SAMBA_VERSION_RELEASE,)

SAMBA_VERSION="$SAMBA_VERSION_MAJOR.$SAMBA_VERSION_MINOR.$SAMBA_VERSION_RELEASE"
AC_SUBST(SAMBA_VERSION)
SAMBA_VERSION_NUMBER=`printf '%d%02d%02d' $SAMBA_VERSION_MAJOR $SAMBA_VERSION_MINOR $SAMBA_VERSION_RELEASE`
AC_SUBST(SAMBA_VERSION_NUMBER)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
CPPFLAGS="$CPPFLAGS -I\$(SOURCE_DIR)/include"
SAMBA_CPPFLAGS="`echo \" $SAMBA_CPPFLAGS \" |sed 's#-I. #-I\$(SAMBA_SOURCE_DIR) #g;s# -I\./# -I\$(SAMBA_SOURCE_DIR)/#g'`"

## Output
## ======================================================================

AC_OUTPUT

chmod +x build/subst

AC_MSG_RESULT([
Configuration summary for $PACKAGE_NAME $PACKAGE_VERSION:

 Samba source:	$SAMBA_SOURCE_DIR
 Samba version:	$SAMBA_VERSION
 Samba prefix:	$SAMBA_prefix
])

